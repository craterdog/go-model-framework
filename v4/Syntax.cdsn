!>
................................................................................
.    Copyright (c) 2009-2024 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
GO CLASS MODEL NOTATION
This document contains a formal definition of the Go Class Model Notation™
(GCMN) using Crater Dog Syntax Notation™ (CDSN):
 * https://github.com/craterdog/go-grammar-framework/blob/main/v4/cdsn/Syntax.cdsn

A language syntax consists of a set of rule definitions and token definitions.

The following intrinsic character types are context specific:
 * ANY - Any language specific character.
 * LOWER - Any language specific lowercase character.
 * UPPER - Any language specific uppercase character.
 * DIGIT - Any language specific digit.
 * ESCAPE - Any environment specific escape sequence.
 * CONTROL - Any environment specific (non-printable) control character.
 * EOL - The environment specific end-of-line character.
 * EOF - The environment specific end-of-file marker (pseudo character).

A predicate may be constrained by any of the following cardinalities:
 * predicate{M} - Exactly M instances of the specified predicate.
 * predicate{M..} - M or more instances of the specified predicate.
 * predicate{M..N} - M to N instances of the specified predicate.
 * predicate? - Zero or one instances of the specified predicate.
 * predicate* - Zero or more instances of the specified predicate.
 * predicate+ - One or more instances of the specified predicate.

An inversion "~" within a definition may only be applied to an intrinsic
character type or a glyph range.
<!

!>
RULE DEFINITIONS
The following rules are used by the parser when parsing the stream of tokens
generated by the scanner.  Each rule name begins with an uppercase letter.
The rule definitions may specify the names of tokens or other rules and are
matched by the parser in the order listed.  A rule definition may also be
directly or indirectly recursive.  The sequence of factors within in a rule
definition may be separated by spaces which are ignored by the parser.
<!
AST: Model EOL* EOF  ! Terminated with an end-of-file marker.

Model: Notice Header Modules? Types? Functionals? Aspects? Classes? Instances?

Notice: comment

Header: comment "package" identifier

Modules: "import" "(" Module* ")"

Module: identifier text

Types: "// Types" Type+

Type: Declaration Abstraction Enumeration?

Declaration: comment "type" identifier ("[" Parameters "]")?

Parameters: Parameter ("," Parameter)* ","?

Parameter: identifier Abstraction

Abstraction: Prefix? identifier ("[" Arguments "]")?

Prefix:
    "[" "]"
    "map" "[" identifier "]"
    "chan"
    identifier "."

Arguments: Abstraction ("," Abstraction)* ","?

Enumeration: "const" "(" Parameter "=" "iota" identifier* ")"

Functionals: "// Functionals" Functional+

Functional: Declaration "func" "(" Parameters? ")" Result

Result: Abstraction | "(" Parameters ")"

Aspects: "// Aspects" Aspect+

Aspect: Declaration "interface" "{" Methods "}"

Classes: "// Classes" Class+

Class: Declaration "interface" "{" Constants? Constructors? Functions? "}"

Constants: "// Constants" Constant+

Constant: identifier "(" ")" Abstraction

Constructors: "// Constructors" Constructor+

Constructor: identifier "(" Parameters? ")" Abstraction

Functions: "// Functions" Function+

Function: identifier "(" Parameters? ")" Result

Instances: "// Instances" Instance+

Instance: Declaration "interface" "{" Attributes? Abstractions? Methods? "}"

Attributes: "// Attributes" Attribute+

Attribute: identifier "(" Parameter? ")" Abstraction?

Abstractions: "// Abstractions" Abstraction+

Methods: "// Methods" Method+

Method: identifier "(" Parameters? ")" Result?

!>
TOKEN DEFINITIONS
The following token definitions are used by the scanner to generate the
stream of tokens that are processed by the parser.  Each token name begins
with a lowercase letter.  Unlike with rule definitions, a token definition
cannot specify the name of a rule within its definition but it can specify
the name of other tokens.  Token definitions cannot be recursive and the
scanning of tokens is NOT greedy.  And any spaces within a token definition
are NOT ignored.
<!
comment: "/*" ANY* "*/"  ! Chooses the shortest possible match.

delimiter: "[" | "]" | "(" | ")" | "{" | "}" | "." | "," | "="

identifier: (LOWER | UPPER) (LOWER | UPPER | DIGIT)*

note: "//" ~[CONTROL]*

text: '"' ANY* '"'  ! Chooses the shortest possible match.

